grid ← {(∨\' '≠⍵)/⍵}¨↓⍉↑(⊃¨{~×4|2+⍳≢⍵}⊆⊢)¨8↑⊃⎕NGET 'input.txt' 1 
ins ← (⍎¨{⍵∊⎕D}⊆⊢)¨10↓⊃⎕NGET 'input.txt' 1

move1 ← { 
  lr ← (1⌷⍺) (↑{(⌽⍺)⍵}↓) ⊃⍵[2⌷⍺]
  ⊃¨({⊂,/(⊃1⌷lr),⊃⍵} @ (3⌷⍺)) ((⊂2⌷lr) @ (2⌷⍺)) ⊂¨⍵
}
move2 ← { 
  lr ← (1⌷⍺) (↑{⍺⍵}↓) ⊃⍵[2⌷⍺]
  ⊃¨({⊂,/(⊃1⌷lr),⊃⍵} @ (3⌷⍺)) ((⊂2⌷lr) @ (2⌷⍺)) ⊂¨⍵
}

⊃¨ins{0=≢⍺:⍵ ⋄ (1↓⍺)∇(⍵ move1⍨ ⊃⍺)}grid
⊃¨ins{0=≢⍺:⍵ ⋄ (1↓⍺)∇(⍵ move2⍨ ⊃⍺)}grid

⍝Refactored
(grid ins)←(×⍤≢¨⊆⊢)⊃⎕NGET 'input.txt' 1
grid←(~∘' ')¨↓⍉⊃¨↑({~×4|2+⍳≢⍵}⊆⊢)¨¯1↓grid
ins←⌽(⍎¨{⍵∊⎕D}⊆⊢)¨ins
move ← { 
  (q f t)←⍺ ⋄ (l r)←q(↑{⍺⍵}↓)⊃⍵[f] 
  ((⍺⍺l)∘,¨@t) (r⍨¨@f)⍵
}
,/⊃¨⊃ ⌽move/ins,⊂grid
,/⊃¨⊃ ⊣move/ins,⊂grid
